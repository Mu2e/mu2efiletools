#!/usr/bin/perl -w
#
# A.Gaponenko, 2016
#


package DatasetEffSummary;
use strict;

sub fill {
    my ($self, $json) = @_;
    my $nfiles = $self->nfiles + 1;
    $self->nfiles($nfiles);

    die "Error: no dh.gencount in metadata for file " . $json->{'file_name'} . "\n"
        unless defined $json->{'dh.gencount'};

    my $genevents = $self->genevents + $json->{'dh.gencount'};
    $self->genevents($genevents);

    # Due to a bug in SAM (INC000001108858) event counts zero does not get
    # stored.  Workaround that with //0.
    my $passedevents = $self->passedevents + ($json->{'event_count'}//0);
    $self->passedevents($passedevents);

    return $self;
}

use Class::Struct DatasetEffSummary => [dsname=>'$', nfiles=>'$', genevents=>'$', passedevents=>'$'];


package main;
use strict;

use English '-no_match_vars';
use Fcntl;

use List::Util qw(min);

use Getopt::Long;
use LWP::UserAgent;
use HTTP::Request::Common;
use JSON;
use File::Basename;
use File::stat;
use Cwd 'abs_path';
use Time::HiRes qw(gettimeofday tv_interval);
use Digest;
use Mu2eDSName;
use Data::Dumper;

use lib dirname($0);
use Mu2eSWI;



my $default_verbosity=2;
my $default_chunkSize = 1000;
my $default_header = 'TABLE SimEfficiencies2';

my $verbosity = $default_verbosity;
my $chunkSize = $default_chunkSize;
my $header = $default_header;

my $outfile = '';

my $sw = Mu2eSWI->new;

my %opt = ( 'outfile' => \$outfile,
            'header' => \$header,
            'writeFullDatasetName' => 0,
            'chunkSize' => \$chunkSize,
            'verbosity' => \$verbosity,
            'help' => 0,
            %{$sw->optDefaults},
    );


#================================================================
sub processDataset {
    my $dsname = shift;

    # make sure cmdline parameters make sense
    Mu2eDSName->parse($dsname);

    my $summary = DatasetEffSummary->new(dsname=>$dsname, nfiles=>0, genevents=>0, passedevents=>0);

    my @fl = $sw->listFiles("dh.dataset=$dsname with availability anylocation");
    my $numFilesTotal = scalar(@fl);

    die "Error: there are no records matching dataset name $dsname\n"
        unless $numFilesTotal;

    print "Processing dataset  $dsname with $numFilesTotal files\n" if $verbosity > 0;

    for(my $numProcessed = 0; $numProcessed < $numFilesTotal; $numProcessed += $chunkSize) {
        my @indexes = ( $numProcessed .. min($numProcessed + $chunkSize, $numFilesTotal) - 1);
        my @chunk = @fl [ @indexes ];
        #print "Got indexes = @indexes    \tchunk = @chunk\n";


        my $res = $sw->ua->request(
            POST $sw->samweb_server.'/sam/mu2e/api/files/metadata',
            Content => { 'file_name' => \@chunk }
            );

        print Dumper($res),"\n" if $verbosity > 9;

        my $jstop = decode_json ($res->content);

        foreach my $js (@$jstop) {
            $summary->fill($js);
        }

        print "\teff = ",
        ($summary->passedevents /  $summary->genevents),
        " (", $summary->passedevents, " / ", $summary->genevents, ")",
        " after processing ",$summary->nfiles,
        " files of ",$summary->dsname,
        "\n"
            if $verbosity > 1;
    }

    return $summary;
}

#================================================================
sub usage() {
    my $self = basename($0);
    return <<EOF
Compute and print out the overall filter effiency for a dataset,
which is the ratio of the number of events in the dataset to
the total number of events generated in the initial stage of the
simulation, in the jobs that ran with the EmptyEvent source.

Usage:
        $self --outfile=<filename>  [options] <DatasetName> [<DatasetName2> ...]

Options:
EOF
.  #$sw->optDocString(' 'x4, ' 'x7) .
<<EOF

    --outfile=<filename>    Proditions-formatted results will be written there.

    --header=<string>       A text to be used for the first line of the file.
                            The default is '$default_header'.

    --writeFullDatasetName  Switch to writing full dataset names to the output file
                            instead of the default dataset description field.

    --chunkSize=<int>       The number of metadata to request in a single SAMWEB transaction.
                            The default is $default_chunkSize.

    --verbosity=<int>       Set verbosity level. The default is $default_verbosity.

    --help                  Print this message.
EOF
;
}

# Process command line opts.
GetOptions(\%opt,
    "outfile=s",
    "header=s",
    "writeFullDatasetName",
    "chunkSize=i",
    "verbosity=i",
    "help",
           @{$sw->optSpec},
           )
    or die "\nError processing command line options.\n";

if($opt{'help'}) {
    print usage();
    exit 0;
}

die "ERROR: At least one dataset name must be specified.  Try the --help option.\n"
    unless $#ARGV >=  0;


die "ERROR: an output file must be specified. Try the --help option\n"
    unless $outfile;

sysopen(my $outfh, $outfile, O_WRONLY | O_EXCL | O_CREAT)
    or die "Error creating $outfile: $!\n";

print $outfh $header . "\n";
foreach my $ds (@ARGV) {
    my $res = processDataset($ds);

    my $ds = Mu2eDSName->parse($res->dsname);
    my $dstag = $opt{'writeFullDatasetName'} ? $ds->dsname : $ds->description;

    # The Proditions table row:
    # Row(std::string tag, unsigned long numerator, unsigned long denominator, double eff)
    print $outfh $dstag
    . ",\t" . $res->passedevents
    . ",\t" . $res->genevents
    . ",\t" . ($res->passedevents /  $res->genevents)
    . "\n";
}

close $outfh or die "Error closing output file $outfile: $!\n";

exit 0;

#================================================================
